10 'allows loading of co files from the serial port in hex format
11 ' TODO: Send loadhx.do to the machine instead. Let's auto-generate that in the future.
20 'com stats
21 cs$ = "COM:88N1E"
22 'start address
23 sa% = -22272

50 ad% = sa%
60 open cs$ for input as 1

100 'wait for colon
110 c$ = input$(1,1)
120 if c$ <> ":" then goto 110
130 gosub 1000
140 bc% = va%
141 print ""
142 print bc%;" ";
143 if bc% = 0 then goto 300
145 sum% = 0
150 'don't care about address or type
160 gosub 1000 'address byte 1
170 gosub 1000 'address byte 2
180 gosub 1000 'type byte
190 for b% = 1 to bc%
200   gosub 1000
201   print ".";
210   poke ad%, va%
220   ad% = ad% + 1
230   'sum% = sum% + va%
235   'if sum% > 255 then sum% = sum% - 255
240 next b%
250 gosub 1000 'read checksum bytes
259 'checksum not yet working. Gotta take 2's compliment.
260 'if va% <> sum% then print "chk warning ";va%;"";sum%
270 goto 100

300 print "done"
305 close 1
310 end

1000 'read hex pair
1010 c$ = input$(1,1)
1020 gosub 2000
1030 va% = (dv% * 16)
1040 c$ = input$(1,1)
1050 gosub 2000
1060 va% = va% + dv%
1070 return

2000 'parse c$ hex into dv%
2010 v% = asc(c$)
2029 ' ascii 0-9 is 48-57, ascii A-F is 65-70, add 10 since hex A is 10
2030 if v% <= 57 then dv% = v% - 48 else dv% = (v% - 65) + 10
2040 return
